plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

repositories {
    mavenCentral()
    maven { url = uri('https://jitpack.io') }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

dependencies {
    implementation fileTree(dir: 'deps', include: ['*.jar'])
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'net.dv8tion:JDA:4.3.0_339'
    implementation 'io.netty:netty:3.6.6.Final'
    implementation 'org.jfree:jfreechart:1.5.5'
    implementation 'com.squareup.okhttp3:okhttp:3.13.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.1'
    implementation 'com.formdev:flatlaf:3.6'
    implementation 'com.squareup.okio:okio:1.17.2'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation 'log4j:log4j:1.2.17'
}

application {
    mainClass.set("server")
}
tasks.register("printRuntimeClasspath") {
    group = "help"
    description = "Prints the full runtime classpath, including all dependencies"

    doLast {
        println configurations.runtimeClasspath.asPath
    }
}
tasks.register("copyRuntimeDeps") {
    group = "build"
    description = "Copies all runtime dependencies into the deps folder"

    // Destination folder
    def depsDir = file("$buildDir/deps")

    doLast {
        // Make sure the folder exists
        if (!depsDir.exists()) {
            depsDir.mkdirs()
        }

        // Copy each runtime dependency jar
        configurations.runtimeClasspath.each { file ->
            if (file.name.endsWith(".jar")) {
                copy {
                    from file
                    into depsDir
                }
            }
        }

        println "Copied ${configurations.runtimeClasspath.files.size()} jars to ${depsDir}"
    }
}

tasks.register('runFileServer', JavaExec) {
    group = 'application'
    description = 'Runs the FileServer'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.Ghreborn.jagcached.FileServer")
}

tasks.register('runGuardianServer', JavaExec) {
    group = 'application'
    description = 'Runs the GuardianServer'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.mystycdh.GuardianServer")
}

sourceSets {
    main {
        java {
            srcDirs = ['src', 'src/main/java']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}
